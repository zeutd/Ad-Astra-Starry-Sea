plugins {
    id 'java' apply true
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    String modLoader = project.name
    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name "createmod maven"
            url "https://maven.createmod.net/"
        }
        mavenCentral()
        maven{ url "https://maven.neoforged.net/releases/" }
        maven{ url "https://maven.firstdarkdev.xyz/snapshots" }
        maven{ url "https://maven.resourcefulbees.com/repository/maven-public/" }
        maven {
            // Location of the maven that hosts Team Resourceful's jars.
            name = "Resourceful Bees Maven"
            url = "https://nexus.resourcefulbees.com/repository/maven-public/"
        }
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup("curse.maven")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = uri("https://api.modrinth.com/maven")
                }
            }
            filter {
                includeGroup("maven.modrinth")
            }
        }
        maven {
            // Location of the maven that hosts Team Resourceful's jars.
            name = "Team Resourceful Maven"
            url = "https://maven.teamresourceful.com/repository/maven-public/"
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()

        modApi(group: "com.teamresourceful.resourcefullib", name: "resourcefullib-$modLoader-$minecraft_version", version: resourcefulLibVersion)
        modApi(group: "com.teamresourceful.resourcefulconfig", name: "resourcefulconfig-$modLoader-$minecraft_version", version: resourcefulConfigVersion)
        modApi(group: "earth.terrarium.botarium", name: "botarium-$modLoader-$minecraft_version", version: botariumVersion)

        compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
        implementation(include("io.github.llamalad7:mixinextras-$modLoader:0.5.0"))

        modImplementation group: "earth.terrarium.adastra", name: "ad_astra-$modLoader-$minecraft_version", version: ad_astra_version, transitive: false
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
